//
// This file is to be applied to every subproject.
//

apply plugin: 'java'
apply plugin: 'maven'

String mavenGroupId = 'ss.sonya'
String mavenVersion = '1.0'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral();
    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

String mavenArtifactId = name

group = mavenGroupId
version = mavenVersion

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId = mavenGroupId
        artifactId = mavenArtifactId
        version = mavenVersion
    }
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}

def vSpring = '4.3.7.RELEASE'
def vSpringSecurity = '4.2.2.RELEASE'
def vHibernate = '5.2.9.Final'
def vHibernateValidator = '5.4.1.Final'
def vMySQLConnector = '6.0.6'
def vDBCP2 = '2.1.1'
def vLog4j = '1.2.17'
def vJavaxEl = '3.0.1-b08'
def vJavaxWeb = '7.0'
def vOrgJson = '20160810'
def vJackson = '2.9.0.pr2'
def vApacheFileUpload = '1.3.2'

def vJUnit = '4.12'

ext.libraries = [
    spring: [
        'org.springframework:spring-context:' + vSpring,
        'org.springframework:spring-tx:' + vSpring,
        'org.springframework:spring-orm:' + vSpring,
        'org.springframework:spring-webmvc:' + vSpring
    ],
    spring_security: [
        'org.springframework.security:spring-security-core:' + vSpringSecurity,
        'org.springframework.security:spring-security-web:' + vSpringSecurity,
        'org.springframework.security:spring-security-config:' + vSpringSecurity
    ],
    test: [
        'org.springframework:spring-test:' + vSpring,
        'junit:junit:' + vJUnit
    ],
    hibernate: [
        'org.hibernate:hibernate-core:' + vHibernate,
        'org.hibernate:hibernate-c3p0:' + vHibernate,
        'org.hibernate:hibernate-validator:' + vHibernateValidator
    ],
    jdbc: [
        'mysql:mysql-connector-java:' + vMySQLConnector,
        'org.apache.commons:commons-dbcp2:' + vDBCP2
    ],
    logging: [
        'log4j:log4j:' + vLog4j
    ],
    javax: [
        'org.glassfish:javax.el:' + vJavaxEl,
        'javax:javaee-web-api:' + vJavaxWeb
    ],
    json: [
        'org.json:json:' + vOrgJson
    ],
    jackson: [
        'com.fasterxml.jackson.core:jackson-core:' + vJackson,
        'com.fasterxml.jackson.core:jackson-annotations:' + vJackson,
        'com.fasterxml.jackson.core:jackson-databind:' + vJackson
    ],
    apache_commons: [
        'commons-fileupload:commons-fileupload:' + vApacheFileUpload
    ]
]

dependencies {
    // Adding dependencies here will add the dependencies to each subproject.
    testCompile libraries.test
}
